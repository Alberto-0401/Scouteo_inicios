plugins {
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0' //Plugin que ayuda a construir proyectos JAVAFX
}

repositories {
	mavenCentral()
}

javafx {
    version = "25"
    modules = [ 'javafx.base', 'javafx.swing', 'javafx.graphics', 'javafx.controls', 'javafx.fxml', 'javafx.media', 'javafx.web']
}

dependencies {
    //No es necesario incluir las dependencias porque ya lo hace el plugin por nosotros
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'org.controlsfx:controlsfx:11.2.1'
    implementation 'eu.hansolo:tilesfx:21.0.9'

    // JasperReports dependencies - Version 7.0.3
    // Excluimos xml-apis y xalan para evitar conflictos con los módulos de Java
    implementation('net.sf.jasperreports:jasperreports:7.0.3') {
        exclude group: 'xml-apis', module: 'xml-apis'
        exclude group: 'xalan', module: 'xalan'
        exclude group: 'xerces', module: 'xercesImpl'
    }
    implementation('net.sf.jasperreports:jasperreports-pdf:7.0.3') {
        exclude group: 'xml-apis', module: 'xml-apis'
        exclude group: 'xalan', module: 'xalan'
        exclude group: 'xerces', module: 'xercesImpl'
    }
    implementation('net.sf.jasperreports:jasperreports-charts:7.0.3') {
        exclude group: 'xml-apis', module: 'xml-apis'
        exclude group: 'xalan', module: 'xalan'
        exclude group: 'xerces', module: 'xercesImpl'
    }
}

java { //Se indica la versión de JDK que ha de utilizar
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
    }
}

application { //Definimos clase principal del proyecto
    mainClass = 'com.javafx.scouteo.Main'
}

sourceSets {
  main {
    resources {
        srcDirs = ["src/main/resources"]
        includes = ["**/*.fxml","**/*.png","**/*.properties","**/*.css","**/*.jasper","**/*.jrxml"]  //Importante para poder abrir una extensión no reconocida
    }
  }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

run {
    jvmArgs = [
        '--add-exports', 'javafx.graphics/com.sun.javafx.sg.prism=ALL-UNNAMED',
        '--add-exports', 'javafx.graphics/com.sun.javafx.sg.prism.web=ALL-UNNAMED',
        '--add-exports', 'javafx.web/com.sun.javafx.webkit=ALL-UNNAMED',
        '--add-exports', 'javafx.web/com.sun.webkit=ALL-UNNAMED',
        '--add-exports', 'javafx.web/com.sun.javafx.webkit.prism=ALL-UNNAMED',
        '--add-opens', 'javafx.graphics/com.sun.javafx.sg.prism=ALL-UNNAMED',
        '--add-opens', 'javafx.graphics/javafx.scene=ALL-UNNAMED',
        '--add-opens', 'javafx.web/javafx.scene.web=ALL-UNNAMED'
    ]
}

// Tarea para copiar todas las dependencias a la carpeta build/libs/lib
tasks.register('copyDependencies', Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/libs/lib"
}

// Configuración del JAR normal
tasks.jar {
    dependsOn copyDependencies // Hace que siempre se copien las librerías al hacer el jar

    manifest {
        attributes(
            'Main-Class': application.mainClass.get(),
            // Esto crea el "Class-Path" dentro del manifiesto del JAR
            'Class-Path': configurations.runtimeClasspath.files.collect { "lib/" + it.name }.join(' ')
        )
    }
}





